Import("env")
Import("conf")
Import("objs")

objs.append(env.Object(source = [
	File(Glob("fec/*.c")),
	File(Glob("log/*.c")),
	File(Glob("crypto/*.c")),
	File(Glob("freertos/*.c")),
	File(Glob("freertos/portable/%s/*.c" % env["FREERTOS_PORT"])),
	File("freertos/portable/MemMang/heap_3.c"),
]))

env.AddSourceIf("LIB_LINEEDIT", File(Glob("cli/lineedit/*.c")), Dir("cli/lineedit"))
env.AddSourceIf("LIB_WOLFMQTT", File(Glob("other/wolfmqtt/src/*.c")), Dir("other/wolfmqtt"))
env.AddSourceIf("LIB_NANOPB", File(Glob("other/nanopb/*.c")), Dir("other/nanopb"))
env.AddSourceIf("LIB_HEATSHRINK", [File("other/heatshrink/heatshrink_encoder.c"), File("other/heatshrink/heatshrink_decoder.c")], Dir("other/heatshrink"))
env.AddSourceIf("LIB_LIBUXB", File(Glob("other/uxb/*.c")), Dir("other/uxb"))
env.AddSourceIf("LIB_SFFS", File(Glob("filesystem/sffs/*.c")), Dir("filesystem/sffs"))
env.AddSourceIf("LIB_TREECLI", File(Glob("cli/treecli/*.c")), Dir("cli/treecli"))
env.AddSourceIf("LIB_DUKTAPE", File(Glob("script/duktape/src-custom/duktape.c")), Dir("script/duktape/src-custom"))
env.AddSourceIf("LIB_SPIFFS", File(Glob("filesystem/spiffs/src/*.c")), Dir("filesystem/spiffs/src"))

duktape = env.Command(
	source = None,
	target = [File("script/duktape/src-custom/duktape.h"), File("script/duktape/src-custom/duktape.c")],
	action = "python lib/script/duktape/tools/configure.py --output-directory lib/script/duktape/src-custom --source-directory lib/script/duktape/src-input --config-metadata lib/script/duktape/config --option-file lib/script/duktape/config/examples/low_memory.yaml -DDUK_USE_FASTINT --rom-support",
)

env.Append(CPPPATH = [
	Dir("fec"),
	Dir("log"),
	Dir("crypto"),
	Dir("freertos/include"),
	Dir("freertos/portable/%s" % env["FREERTOS_PORT"]),
	Dir("libopencm3/include"),

])

env.Append(LIBPATH = [
	Dir("libopencm3/lib"),
])

# nanopb requirement for large fields
env.Append(CFLAGS = ["-DPB_FIELD_16BIT"])
